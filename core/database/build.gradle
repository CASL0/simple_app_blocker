import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    namespace 'jp.co.casl0.android.simpleappblocker.core.database'
    compileSdk libs.versions.compileSdk.get().toInteger()
    buildToolsVersion = libs.versions.buildTools.get()

    defaultConfig {
        minSdk 26
        targetSdk libs.versions.targetSdk.get().toInteger()
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        managedDevices {
            devices {
                pixel6api33(ManagedVirtualDevice) {
                    device = "Pixel 6"
                    apiLevel = 33
                    systemImageSource = "google"
                    require64Bit = false
                }
                pixel6api29(ManagedVirtualDevice) {
                    device = "Pixel 6"
                    apiLevel = 29
                    systemImageSource = "google"
                    require64Bit = false
                }
            }
        }
    }
}

dependencies {
    implementation project(":core:model")
    implementation libs.androidx.room.ktx
    annotationProcessor libs.androidx.room.compiler
    kapt libs.androidx.room.compiler
    implementation libs.com.google.dagger.hilt.android
    kapt libs.com.google.dagger.hilt.compiler
    implementation libs.org.jetbrains.kotlinx.datetime

    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.ext.junit.ktx
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.room.testing
}
