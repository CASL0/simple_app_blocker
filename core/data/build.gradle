import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlinx.kover'
}

android {
    namespace 'jp.co.casl0.android.simpleappblocker.core.data'
    compileSdk libs.versions.compileSdk.get().toInteger()
    buildToolsVersion = libs.versions.buildTools.get()

    defaultConfig {
        minSdk 26
        targetSdk libs.versions.targetSdk.get().toInteger()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        managedDevices {
            devices {
                pixel6api33(ManagedVirtualDevice) {
                    device = "Pixel 6"
                    apiLevel = 33
                    systemImageSource = "google"
                    require64Bit = false
                }
                pixel6api29(ManagedVirtualDevice) {
                    device = "Pixel 6"
                    apiLevel = 29
                    systemImageSource = "google"
                    require64Bit = false
                }
            }
        }
    }
}

dependencies {
    implementation project(":core:model")
    implementation project(":core:database")
    implementation project(":core:common")
    implementation libs.org.jetbrains.kotlinx.coroutines.android
    implementation libs.androidx.room.ktx
    implementation libs.com.google.dagger.hilt.android
    kapt libs.com.google.dagger.hilt.compiler
    testImplementation libs.junit
    testImplementation libs.org.hamcrest.all
    testImplementation libs.org.jetbrains.kotlinx.coroutines.test

    androidTestImplementation libs.androidx.arch.core.testing
    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.ext.junit.ktx
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.org.jetbrains.kotlinx.coroutines.test
}
