plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'kotlin-parcelize'
    id 'org.jmailen.kotlinter'
    id 'org.jetbrains.kotlinx.kover'
}

android {
    namespace "jp.co.casl0.android.simpleappblocker"
    compileSdk libs.versions.compileSdk.get().toInteger()
    buildToolsVersion = libs.versions.buildTools.get()

    defaultConfig {
        applicationId "jp.co.casl0.android.simpleappblocker"
        minSdk 29
        targetSdk libs.versions.targetSdk.get().toInteger()
        versionCode libs.versions.versionCode.get().toInteger()
        versionName libs.versions.versionName.get()
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {
    implementation project(":core:model")
    implementation project(":core:data")
    implementation project(":feature:update")
    implementation project(":feature:about")
    implementation project(":feature:blocklog")
    implementation project(":feature:packet_filtering")
    implementation project(":feature:rule_change")
    implementation project(":feature:allowlist")
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.navigation.ui.ktx
    implementation libs.com.orhanobut.logger
    implementation libs.com.google.android.play.app.update
    implementation libs.com.google.dagger.hilt.android
    implementation libs.androidx.core.splashscreen
    kapt libs.com.google.dagger.hilt.compiler

    kover(project(":core:data"))
    kover(project(":feature:rule_change"))
    kover(project(":feature:blocklog"))
}
