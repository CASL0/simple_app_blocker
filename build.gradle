buildscript {
    dependencies {
        classpath libs.com.google.dagger.hilt.android.gradle.plugin
        classpath libs.com.google.android.gms.oss.licenses.plugin
        classpath libs.com.twitter.compose.rules.ktlint
    }
}// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    alias(libs.plugins.com.android.application) apply false
    alias(libs.plugins.com.android.library) apply false
    alias(libs.plugins.kotlin) apply false
    alias(libs.plugins.de.undercouch.download)
    alias(libs.plugins.org.jmailen.kotlinter) apply false
    alias(libs.plugins.com.vanniktech.dependency.graph.generator)
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task fetchLibs(type: Download) {
    def version = libs.versions.pcapplusplus.get()
    src "https://github.com/seladb/PcapPlusPlus/releases/download/v$version/pcapplusplus-$version-android.tar.gz"
    dest new File(buildDir, 'pcapplusplus-android.tar.gz')
}

task expandLibs(dependsOn: fetchLibs, type: Copy) {
    def version = libs.versions.pcapplusplus.get()
    from tarTree(fetchLibs.dest)
    into new File(projectDir, 'core/pcapplusplus/libs/')
    filesMatching("**/pcapplusplus-$version-android/**/*") {
        it.path = it.path.replace("pcapplusplus-$version-android", "pcapplusplus")
    }
}

task setup(dependsOn: expandLibs, type: Delete) {
    def version = libs.versions.pcapplusplus.get()
    delete new File(projectDir, "core/pcapplusplus/libs/pcapplusplus-$version-android")
}
